buildscript {
    ext {
        guavaVersion = '+'
        mysqlVersion = '8.+'
        pgsqlVersion = '42.+'
        jsqlparserVersion = '+'
        freemarkerVersion = '+'
        commonsPool2 = '+'
        saTokenVersion = '+'
        easyexcelVersion = '+'
        disruptorVersion = '3.+'
        springdocVersion = '+'
        caffeineVersion = '3.+'
        retrofitJacksonVersion = '+'
        springBootVersion = '2.+'
        kafkaVersion = '+'
        flywayVersion = '8.4.+'
        lombokVersion = "+"
        pdfboxVersion = "2.+"
        tencentcloudVersion = "3.1.431"
        baiduVersion = "4.+"
        aliyunPublic = 'https://maven.aliyun.com/repository/public/'
        aliyunGoogle = 'https://maven.aliyun.com/repository/google'
        aliyunGradlePlugin = 'https://maven.aliyun.com/repository/gradle-plugin'
        aliyunSpring = 'https://maven.aliyun.com/repository/spring'
        aliyunSpringPlugin = 'https://maven.aliyun.com/repository/spring-plugin'
//        incarMavenRepoUrl='https://repository.incarcloud.com/content/repositories/snapshots/'
    }

    repositories {
        mavenLocal()
        maven { url "${aliyunPublic}" }
        maven { url "${aliyunGradlePlugin}" }
        maven { url "${aliyunSpringPlugin}" }
        mavenCentral()
    }

    dependencies {
        //使用springboot框架
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: "${springBootVersion}"
        //使用flyway插件
        classpath group: 'org.flywaydb', name: 'flyway-gradle-plugin', version: "${flywayVersion}"
    }
}

/**
 * ----------------------------------------------------------------------------
 */

group 'com.bcd'
version '1.0'
apply plugin: 'java-library'
apply plugin: 'java'
apply plugin: 'idea'
//加入maven发布依赖
apply plugin: 'maven-publish'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenLocal()
    maven { url "${aliyunPublic}" }
    maven { url "${aliyunGoogle}" }
    maven { url "${aliyunSpring}" }
    mavenCentral()
//    maven {
//        url "${incarMavenRepoUrl}"
//        //允许http依赖库
//        allowInsecureProtocol = true
//    }
}

//下载源码和文档
//gradle cleanIdea idea
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

//去除springboot默认的logback
configurations.all {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    exclude module: 'logback-classic'
    exclude module: 'logback-core'
}

dependencies {
    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
    // https://mvnrepository.com/artifact/com.google.code.findbugs/annotations
    implementation group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'

    // https://mvnrepository.com/artifact/com.google.guava/guava
    implementation group: 'com.google.guava', name: 'guava', version: "${guavaVersion}"

    //springboot支持
    implementation("org.springframework.boot:spring-boot-starter")
    implementation("org.springframework.boot:spring-boot-configuration-processor")
    //springboot cache支持
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
    //springboot aop支持
    implementation("org.springframework.boot:spring-boot-starter-aop")
    //springboot web支持
    implementation("org.springframework.boot:spring-boot-starter-web")


    //springboot log4j2支持
    implementation('org.springframework.boot:spring-boot-starter-log4j2')
    //disruptor(log4j2异步日志需要用到)
    implementation group: 'com.lmax', name: 'disruptor', version: "${disruptorVersion}"

    //springboot jpa支持
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    //springboot jdbc支持
    implementation("org.springframework.boot:spring-boot-starter-jdbc")
    implementation("com.github.jsqlparser:jsqlparser:${jsqlparserVersion}")
    //邮件支持
    implementation("org.springframework.boot:spring-boot-starter-mail")

    //springboot redis支持
    //lettuce异步客户端(默认使用lettuce,必须手动引用apache commons-pool2)
    implementation("org.springframework.boot:spring-boot-starter-data-redis")
    implementation("org.apache.commons:commons-pool2:${commonsPool2}")
    //springboot validation支持
    implementation("org.springframework.boot:spring-boot-starter-validation")

    //springboot mongodb支持
    implementation("org.springframework.boot:spring-boot-starter-data-mongodb")

    //spring kafka支持
    implementation("org.springframework.kafka:spring-kafka:${kafkaVersion}")

    // https://mvnrepository.com/artifact/com.alibaba/easyexcel
    implementation group: 'com.alibaba', name: 'easyexcel', version: "${easyexcelVersion}"

    // https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-ui
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: "${springdocVersion}"

    // https://mvnrepository.com/artifact/com.github.ben-manes.caffeine/caffeine
    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: "${caffeineVersion}"

    // https://mvnrepository.com/artifact/org.freemarker/freemarker
    implementation group: 'org.freemarker', name: 'freemarker', version: "${freemarkerVersion}"


    //mysql驱动包
    runtimeOnly "mysql:mysql-connector-java:${mysqlVersion}"
    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    runtimeOnly group: 'org.postgresql', name: 'postgresql', version: "${pgsqlVersion}"


    // https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-jackson
    implementation group: 'com.squareup.retrofit2', name: 'converter-jackson', version: "${retrofitJacksonVersion}"

    testImplementation("org.springframework.boot:spring-boot-starter-test")

    // https://mvnrepository.com/artifact/org.apache.pdfbox/pdfbox
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: "${pdfboxVersion}"

    // https://mvnrepository.com/artifact/com.tencentcloudapi/tencentcloud-sdk-java
    implementation group: 'com.tencentcloudapi', name: 'tencentcloud-sdk-java', version: "${tencentcloudVersion}"

    // https://mvnrepository.com/artifact/com.baidu.aip/java-sdk
    implementation("com.baidu.aip:java-sdk:${baiduVersion}") {
        exclude group: "org.slf4j", module: "slf4j-simple"
    }

    // https://mvnrepository.com/artifact/cn.dev33/sa-token-spring-boot-starter
    implementation group: 'cn.dev33', name: 'sa-token-spring-boot-starter', version: "${saTokenVersion}"
    implementation group: 'cn.dev33', name: 'sa-token-dao-redis-jackson', version: "${saTokenVersion}"


}

//生成jar时打包源码
task sourceJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier "sources"
}

jar {
    enabled = true
}

//发布到本地MAVEN仓库使用 gradle PublishToMavenLocal
//发布到远程MAVEN仓库使用 gradle publishMavenPublicationToMavenRepository
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(sourceJar)
        }
    }
    repositories {
        maven {
            url 'https://repository.incarcloud.com/content/repositories/snapshots/'
            credentials {
                username 'deployment'
                password 'deploy123456'
            }
        }
    }
}

/**
 * SpringBoot 2开始,使用gradle bootJar进行打包,并要配置入口类
 */
bootJar {
    exclude "**/*-dev.yml"
    exclude "**/*-test.yml"
    manifest {
        attributes 'Start-Class': 'com.bcd.Application'
    }
}


/**
 * ----------------------------------------------------------------------------
 */
bootRun {
    /**
     * 启动时候带入jvm的参数;如下(启动jvisualvm监控)
     * nohup gradle bootRun -PjvmArgs="-Djava.rmi.server.hostname=172.16.24.36 -Dcom.sun.management.jmxremote.port=10001 -Dcom.sun.management.jmxremote.ssl=false -Dcom.sun.management.jmxremote.authenticate=true" &
     */
    if (project.hasProperty('jvmArgs')) {
        jvmArgs(project.jvmArgs.split("\\s+") as List)
    }
    //开启缓存行补齐
    jvmArgs('-XX:-RestrictContended')
    jvmArgs('-Dfile.encoding=UTF-8')
    jvmArgs('-DLog4jContextSelector=org.apache.logging.log4j.core.async.AsyncLoggerContextSelector')
}


/**
 * ----------------------------------------------------------------------------
 */

apply plugin: 'org.flywaydb.flyway'

flyway {
    def subLocations = []
    //mysql
    url = 'jdbc:mysql://127.0.0.1:3306/msbf?characterEncoding=utf8&useSSL=false&rewriteBatchedStatements=true&serverTimezone=UTC'
    user = 'root'
    password = '123456'
    subLocations.add('filesystem:' + project.sourceSets.main.resources.srcDirs.iterator().next().getPath() + '/db/migration/mysql')
    //pgsql
//    url = 'jdbc:postgresql://127.0.0.1:5432/msbf'
//    user = 'root'
//    password = '123456'
//    subLocations.add('filesystem:'+project.sourceSets.main.resources.srcDirs.iterator().next().getPath()+'/db/migration/pgsql')

    flyway.locations = subLocations
}

flywayMigrate {
    //是否允许无序的迁移
    outOfOrder = true
}

/**
 * ----------------------------------------------------------------------------
 */
