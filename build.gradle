//所有模块配置
allprojects{
    group 'com.bcd'
    version '1.0-SNAPSHOT'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    //加入maven发布依赖
    apply plugin: 'maven-publish'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        maven { url "${mavenRepoUrl}" }
        maven { url 'https://repository.incarcloud.com/content/repositories/snapshots/' }
//        jcenter()
//        mavenCentral()
    }

    //下载源码和文档
    //gradle cleanIdea idea
    idea {
        module {
            downloadSources=true
//            downloadJavadoc=true
        }
    }
    dependencies{
        //springboot rabbitmq支持
        compile("org.springframework.boot:spring-boot-starter-amqp:${springBootVersion}")
        //springboot kafka支持
        compile("org.springframework.kafka:spring-kafka:${kafkaVersion}")
        //邮件支持
        compile("org.springframework.boot:spring-boot-starter-mail:${springBootVersion}")
        //shiro权限框架集成
        compile("org.apache.shiro:shiro-spring:${shiroVersion}")
        compile("org.apache.shiro:shiro-ehcache:${shiroVersion}")
        //rocketmq集成
        compile("com.aliyun.openservices:ons-client:1.7.0.Final")
        //阿里云 tablestore集成
        compile("com.aliyun.openservices:tablestore:${tablestoreVersion}") {
            //解决阿里云传递性依赖
            exclude group: 'org.apache.logging.log4j', module: 'slf4j-api'
            exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
            exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
        }
        //阿里云mns集成
        compile("com.aliyun.mns:aliyun-sdk-mns:${mnsVersion}")
        //阿里云sms集成
        compile ("com.aliyun:aliyun-java-sdk-dysmsapi:${smsVersion}")
        compile ("com.aliyun:aliyun-java-sdk-core:${aliyunSdkCoreVersion}")
        //阿里云视频直播live集成
        compile ("com.aliyun:aliyun-java-sdk-live:${liveVersion}")
        //阿里云oss对象存储集成
        compile ("com.aliyun.oss:aliyun-sdk-oss:${ossVersion}")
        //flyway支持
        compile("org.flywaydb:flyway-core:${flywayVersion}")
        //ics
        compile ("com.incarcloud.ics:ics-map-tracking:5.3-SNAPSHOT")
        //ics
        compile ("com.incarcloud.vco:VCOService:2.3-SNAPSHOT")
    }

    compileJava{
        options.compilerArgs << "-Xlint:unchecked"
        options.compilerArgs << "-parameters"
        options.encoding = "UTF-8"
        options.compilerArgs << "-Xdiags:verbose"
    }

    compileTestJava{
        options.compilerArgs << "-Xlint:unchecked"
        options.compilerArgs << "-parameters"
        options.encoding = "UTF-8"
        options.compilerArgs << "-Xdiags:verbose"
    }

    jar {
        enabled = true
    }

    /**
     * SpringBoot 2开始,使用gradle bootJar进行打包,并要配置入口类
     */
    bootJar {
        manifest {
            attributes 'Start-Class': 'com.bcd.Application'
        }
    }

    //生成jar时打包源码
    task sourceJar(type: Jar) {
        from sourceSets.main.java.srcDirs
        classifier "sources"
    }

    //发布到本地MAVEN仓库使用 gradle PublishToMavenLocal
    //发布到远程MAVEN仓库使用 gradle publishMavenPublicationToMavenRepository
    publishing{
        publications {
            maven(MavenPublication){
                from components.java
                artifact(sourceJar)
            }
        }
        repositories {
            maven{
                url 'https://repository.incarcloud.com/content/repositories/snapshots/'
                credentials {
                    username 'deployment'
                    password 'deploy123456'
                }
            }
        }
    }
}

apply plugin: 'org.flywaydb.flyway'

flyway {
    url = 'jdbc:mysql://127.0.0.1:3306/msbf?characterEncoding=utf8&useSSL=false&rewriteBatchedStatements=true&serverTimezone=UTC'
    user = 'root'
    password = '123456'
    def subLocations=[]
    for(project in subprojects){
        subLocations.add('filesystem:'+project.sourceSets.main.resources.srcDirs.iterator().next().getPath()+'/db/migration')
    }
    flyway.locations =subLocations
}

flywayMigrate {
    outOfOrder=true
}

bootRun {
    /**
     * 启动时候带入jvm的参数;如下(启动jvisualvm监控)
     * nohup gradle bootRun -PjvmArgs="-Djava.rmi.server.hostname=172.16.24.36 -Dcom.sun.management.jmxremote.port=10001 -Dcom.sun.management.jmxremote.ssl=false -Dcom.sun.management.jmxremote.authenticate=true" &
     */
    if (project.hasProperty('jvmArgs') ) {
        jvmArgs = (project.jvmArgs.split("\\s+") as List)
    }
    //开启缓存行补齐
    jvmArgs('-XX:-RestrictContended')
}

buildscript {
    ext {
        springBootVersion = '+'
        shiroVersion='+'
        kafkaVersion='+'
        tablestoreVersion='+'
        mnsVersion='+'
        smsVersion='+'
        liveVersion='+'
        ossVersion='+'
        aliyunSdkCoreVersion='+'
        flywayVersion = '5.+'
        mavenRepoUrl='https://maven.aliyun.com/repository/central'
    }

    repositories {
        mavenLocal()
        maven { url "${mavenRepoUrl}" }
//        jcenter()
//        mavenCentral()
    }

    dependencies {
        //使用springboot框架
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        //使用flyway插件
        classpath "org.flywaydb:flyway-gradle-plugin:${flywayVersion}"
    }
}

dependencies {
    compile project(':Sys')
    compile project(':MongoDB')
}
